title: ChildStart
---
<<declare $childName = "Lukas">>
<<declare $floristKidName = "Pranas">>
<<zeroChar>>
*{$floristName} moves to the front of the store, preparing to flip the sign from open to closed.*
<<changeSprite 1 1>>
<<oneChar>>
<<lighten>>
{$childName}: Wait!
<<darken>>
*{$floristName} turns around, surprised*
<<lighten>>
{$childName}: Miss Florist, please don’t close yet!
<<darken>>
{$floristName}: ...
<<lighten>>
{$childName}: Please ma'am?
<<darken>>
{$floristName}: Uh, alright then. How can I help you?
<<lighten>>
<<changeSprite 1 0>>
{$childName}: It’s my mom’s birthday soon and I want to get her some flowers!
*{$childName} barely reaches the top of the counter, dropping some bills and coins, some spilling off the table*
{$childName}: Sorry, dad gave me this much. How many flowers can I get?
-> That’s not a lot of money…
    <<jump TellTheTruth>>
-> Um… I’ll see what I can make.
    <<jump LookAtFlowers>>

===

title: TellTheTruth
---
<<lighten>>
<<changeSprite 1 2>>
{$childName}: Oh...
*{$childName} is about to cry*
{$childName}: But this is all I have...
<<darken>>
{$floristName}: Oh, don’t worry, Whatever you get, your mom will love! I promise. Here, I’ll make a pretty bouquet with any three flowers you want!
<<lighten>>
<<changeSprite 1 0>>
{$childName}: Are you sure...?
<<darken>>
{$floristName}: Absolutely.
{$floristName}: (<i>I guess I’ll give him a discount...</i>)
<<jump LookAtFlowers>>
===

title: LookAtFlowers
---
<<darken>>
{$floristName}: Well, what kind of flowers are you looking for?
<<lighten>>
{$childName}: Since they’re for mom, I want one that helps her be strong!
<<darken>>
{$floristName}: Strength, huh? Everyone could use more of that these days...
<<lighten>>
{$childName}: And one to always make her happy!
<<darken>>
{$floristName}: This flower’s a bit thorny, but it’ll always protect the people close to them.
{$floristName}: And what would you like your third flower to be?
<<lighten>>
<<changeSprite 1 1>>
{$childName}: Do you have one that’ll make someone feel better?
<<darken>>
{$floristName}: What do you mean?
<<lighten>>
<<changeSprite 1 0>>
{$childName}: Well, mom's always in the hospital, so I want to help her get better so she can come home!
<<darken>>
-> ...
    <<jump Silence>>
-> Oh, I see...
    <<jump Cracking>>
===

title: Silence
---
*Tears form at {$floristName}'s eyes*
<<lighten>>
<<changeSprite 1 1>>
{$childName}: Um... Miss Florist, are you ok?
<<darken>>
{$floristName}: I’m okay, sorry. So your mom’s sick?
<<lighten>>
<<changeSprite 1 0>>
{$childName}: Yeah, but dad's working really hard! And the <color="yellow">doctors</color> that talk funny have been very nice too!
<<darken>>
{$floristName}: (<i>Talk funny? He must mean the foreign aid workers that recently arrived.</i>)
<<jump FinishBouquet>>
===

title: Cracking
---
<<darken>>
{$floristName}: {$floristKidName}...
<<lighten>>
<<changeSprite 1 0>>
{$childName}: That’s a pretty name, is that a flower?
<<darken>>  
{$floristName}: No, nevermind, sorry. Is your mom ok?
<<lighten>>
{$childName}: I’m not sure, but dad says that medicine is really expensive.
{$childName}: But, I think she’ll get a lot better once she gets the flowers!
<<jump FinishBouquet>>
===

title: FinishBouquet
---
<<darken>>
{$floristName}: Alright, how about these Chamomile?
{$floristName}: It represents patience in adversity.
<<lighten>>
<<changeSprite 1 1>>
{$childName}: I’m not sure what that means.
<<darken>>
{$floristName}: If you wait, your mom will get better. All you have to do is be by her side, alright?
<<lighten>>
<<changeSprite 1 0>>
{$childName}: Yeah! I can do that!
<<darken>>
{$floristName}: Good boy.
===